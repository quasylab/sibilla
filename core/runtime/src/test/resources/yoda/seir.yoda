param nSus = 10;
param nInf = 3;
param er = 0.15;
param ir = 0.25;
param rr = 0.25;
param tolerance = 10.0;
param speed = 1;

agent Agent =
    state:
        bool exposed = false;
        bool infected = false;
        real dir = 0.0;
    features:
        bool contagious = false;
        real x = 0.0;
        real z = 0.0;
    observations:
        bool contact = false;
    actions:
        move [dir <- U[ 0.0 , (2 * PI)];]
        getExposed [dir <- U[ 0.0 , (2 * PI)]; exposed <- true;]
        getInfected [dir <- U[ 0.0 , (2 * PI)]; exposed <- false; infected <- true;]
        recover [dir <- U[ 0.0 , (2 * PI)]; infected <- false;]
    behaviour:
        when contact && (!exposed) && (!infected) -> [ getExposed : er; move : (1-er); ]
        orwhen exposed -> [ getInfected : ir; move : (1-ir);]
        orwhen infected -> [ recover : rr; move : (1-rr);]
        otherwise [ move:1; ]
end

environment :
    sensing:
        Agent [
            contact <- (any Agent : (contagious && (distance(it.x, it.z, x, z) < tolerance)));
        ]
    dynamic:
        Agent [
            contagious <- infected;
            x <- (x + cos(dir)) * speed;
            z <- (z + sin(dir)) * speed;
        ]
end

configuration Main :
    for i sampled distinct nSus time from U[0, (nInf+nSus)/2] do
        Agent [ x = i; z = U[0,50]; ]
    endfor
    for j sampled distinct nInf time from U[0, (nInf+nSus)/2] do
        Agent [ x = j; z = U[0,50]; infected = true; contagious = true;]
    endfor
end

measure infected = #Agent[ infected ];
measure exposed = #Agent[ exposed ];
measure healthy = #Agent[ !(exposed || infected) ];
