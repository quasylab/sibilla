const S = 0;
const E = 1;
const I = 2;
const R = 3;

param nSus = 10;
param nInf = 3;
param er = 0.15;
param ir = 0.25;
param rr = 0.25;
param lr = 0.1;
param tolerance = 10.0;
param speed = 1;

agent Agent =
    state:
        int status = 0;
        real dir = 0.0;
    features:
        bool contagious = false;
        real x = 0.0;
        real z = 0.0;
    observations:
        bool contact = false;
    actions:
        move [dir <- U[ 0.0 , (2 * PI)];]
        getExposed [dir <- U[ 0.0 , (2 * PI)]; status <- E;]
        getInfected [dir <- U[ 0.0 , (2 * PI)]; status <- I;]
        recover [dir <- U[ 0.0 , (2 * PI)]; status <- R;]
        lost [dir <- U[ 0.0 , (2 * PI)]; status <- S;]
    behaviour:
        when contact && (status == S) -> [ getExposed : er; move : (1-er); ]
        orwhen (status == E) -> [ getInfected : ir; move : (1-ir);]
        orwhen (status == I) -> [ recover : rr; move : (1-rr);]
        orwhen (status == R) -> [ lost: lr; move: (1-lr); ]
        otherwise [ move:1; ]
end

environment :
    sensing:
        Agent [
            contact <- (any Agent : (contagious && (distance(it.x, it.z, x, z) < tolerance)));
        ]
    dynamic:
        Agent [
            contagious <- (status == I);
            x <- (x + cos(dir)) * speed;
            z <- (z + sin(dir)) * speed;
        ]
end

configuration Main :
    for i sampled distinct nSus time from U[0, (nInf+nSus)/2] do
        Agent [ x = i; z = U[0,50]; ]
    endfor
    for j sampled distinct nInf time from U[0, (nInf+nSus)/2] do
        Agent [ x = j; z = U[0,50]; status=I; contagious = true;]
    endfor
end

measure infected = #Agent[ (status==I) ];
measure exposed = #Agent[ (status==E) ];
measure healthy = #Agent[ (status==S)||(status==R) ];
measure susceptible = #Agent[ (status==S) ];
measure recovered = #Agent[ (status==R) ];
